actor ShotokanLightBoss : BaseMM8BDMWep_CBM
{//i put this in 'actors' because fuck ARKMAN for making 'wep' a seperate folder and thus not making .txts load as DECORATE files - that's been an issue for 6 years  ~~Max
tag "$TAGC_0R"
dropitem "RisingFistWepCDropped" //HadokenWep kappa
Weapon.AmmoUse 0
Weapon.AmmoGive 28
Weapon.SlotNumber 1
Obituary "$OB_RYUL_W"
Inventory.Pickupmessage "You became, like... the 11th shotoclone or something?"
weapon.ammotype "SuperComboGauge"
weapon.ammotype2 "DrRightAmmo"
States
{
Spawn:
C_00 R 1
loop
Ready:
HDKO B 0 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU)
HDKO B 0 A_GiveInventory("SpawningMisfire",1)
HDKO B 1 A_GunFlash
HDKO B 0 A_SpawnItemEx("RyuL_SelectionFX",0,0,70,0,0,3,0,1)
goto Ready0
Ready0:
HDKO B 2 A_WeaponReady
HDKO B 0 A_TakeInventory("SpawningMisfire",1)
Goto Ready1

Ready1:
HDKO B 2 A_WeaponReady
HDKO B 2 Offset(-2,34)A_WeaponReady(1)
HDKO B 2 Offset(-5,37)A_WeaponReady(1)
HDKO B 2 Offset(-7,41)A_WeaponReady(1)
HDKO B 2 Offset(-9,46)A_WeaponReady(1)
HDKO B 2 Offset(-11,52)A_WeaponReady(1)
HDKO B 2 Offset(-9,46)A_WeaponReady(1)
HDKO B 2 Offset(-7,41)A_WeaponReady(1)
HDKO B 2 Offset(-5,37)A_WeaponReady(1)
HDKO B 2 Offset(-2,34)A_WeaponReady(1)
HDKO B 2 A_WeaponReady
HDKO B 2 Offset(2,34)A_WeaponReady(1)
HDKO B 2 Offset(5,37)A_WeaponReady(1)
HDKO B 2 Offset(7,41)A_WeaponReady(1)
HDKO B 2 Offset(9,46)A_WeaponReady(1)
HDKO B 2 Offset(11,52)A_WeaponReady(1)
HDKO B 2 Offset(9,46)A_WeaponReady(1)
HDKO B 2 Offset(7,41)A_WeaponReady(1)
HDKO B 2 Offset(5,37)A_WeaponReady(1)
HDKO B 2 Offset(2,34)A_WeaponReady(1)
HDKO B 2 A_WeaponReady
HDKO B 2 Offset(-2,34)A_WeaponReady(1)
HDKO B 2 Offset(-5,37)A_WeaponReady(1)
HDKO B 2 Offset(-7,41)A_WeaponReady(1)
HDKO B 2 Offset(-9,46)A_WeaponReady(1)
HDKO B 2 Offset(-11,52)A_WeaponReady(1)
HDKO B 2 Offset(-9,46)A_WeaponReady(1)
HDKO B 2 Offset(-7,41)A_WeaponReady(1)
HDKO B 2 Offset(-5,37)A_WeaponReady(1)
HDKO B 2 Offset(-2,34)A_WeaponReady(1)
HDKO B 2 A_WeaponReady
HDKO B 2 Offset(2,34)A_WeaponReady(1)
HDKO B 2 Offset(5,37)A_WeaponReady(1)
HDKO B 2 Offset(7,41)A_WeaponReady(1)
HDKO B 2 Offset(9,46)A_WeaponReady(1)
HDKO B 2 Offset(11,52)A_WeaponReady(1)
HDKO B 2 Offset(9,46)A_WeaponReady(1)
HDKO B 2 Offset(7,41)A_WeaponReady(1)
HDKO B 2 Offset(5,37)A_WeaponReady(1)
HDKO B 2 Offset(2,34)A_WeaponReady(1)
HDKO B 0 A_JumpIfInventory("RyuSuperFlag",1,2)
HDKO B 0 A_GiveInventory("SuperComboGauge",1)
HDKO B 0
loop
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
HDKO A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
HDKO A 1 A_Raise
Loop

SpawnProtect:
HDKO B 1
Goto Ready0

Fire:
//HDKO S 0 A_JumpIfInventory("IsBot",1,"BotFire")
HDKO S 0 A_JumpIfInventory("SpawningMisfire",1,"SpawnProtect")
HDKO S 0 A_JumpIfInventory("RyuSuperFlag",1,"ShinkuHadoken")
HDKO S 0 A_JumpIfInventory("MegaSlide",1,"Shoryu")
HDKO S 0 A_JumpIfInventory("RyuAltFlag",1,"ShinTatCheck")
HDKO S 0 A_GiveInventory("RyuMainFlag",1)
HDKO S 3 Offset(55,82)A_WeaponReady(5)
HDKO S 0 A_PlaySoundEx("Ryu_L/hadoken","SoundSlot6")
HDKO S 1 Offset(20,52)A_WeaponReady(5)
goto Hadoken
HadokenQuiet:
HDKO S 3 Offset(55,82)
HDKO S 1 Offset(20,52)
goto Hadoken
Hadoken:
HDKO S 0 A_PlaySoundEx("weapon/FireManShot","Weapon")
HDKO S 0 A_GiveInventory("RyuMain_CI",1)
HDKO S 8 Offset(1,31)
HDKO S 16 Offset(20,52)
HDKO S 0 A_Refire("HadokenQuiet")
HDKO S 0 A_TakeInventory("RyuMainFlag",1)
Goto Ready1
ShakuCheck:
HDKO S 0
HDKO S 0 A_JumpIfInventory("SuperComboGauge",7,"EXFire")
HDKO S 0 A_PlaySoundEx("Ryu_L/hadoken","SoundSlot6")
HDKO S 1 Offset(20,52)
goto Hadoken
EXFire:
HDKO S 0 A_PlaySoundEx("Ryu_L/EXMove","Body")
HDKO S 0 A_PlaySoundEx("Ryu_L/shakunetsu","SoundSlot6")
HDKO S 0 A_PlaySoundEx("weapon/atomicfire","Weapon")
HDKO S 0 A_GiveInventory("RyuMainEX_CI",1)
HDKO S 2 Offset(1,31)
HDKO T 2 Offset(1,31)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO S 2 Offset(1,31)ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO T 2 Offset(1,31)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO S 0 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO S 2 Offset(20,52)A_TakeInventory("RyuMainFlag",1)
HDKO T 2 Offset(20,52)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO S 12 Offset(20,52)ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
Goto Ready1
ShinkuHadoken:
HDKO S 0 A_GiveInventory("RyuMainFlagX",1)
HDKO S 0 A_Refire(1)
HDKO S 0 ACS_ExecuteAlways(CORE_ACS_191,0,APROP_Speed,3,5)//2,5
HDKO S 0 A_PlaySoundEx("Ryu_L/shinhado","SoundSlot6")
HDKO S 0 SetPlayerProperty(0,0,0)
HDKO S 0 A_TakeInventory("MegaSlide",9)
HDKO S 0 A_TakeInventory("RyuAltFlag",9)
HDKO S 0 A_TakeInventory("RyuL_Flag",9)
HDKO S 0 A_TakeInventory("SuperComboGauge",999)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 0 A_GiveInventory("BossBasicArmorAmount",40)
HDKO U 0 A_GiveInventory("RyuL_ArmorPickup",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)
HDKO U 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P2",1)
HDKO S 2 Offset(55,82)A_GiveInventory("RyuL_HadokenSuperCharge_P1",1)

HDKO U 2 Offset(55,82)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE2,7)
HDKO S 2 Offset(55,82)ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO U 2 Offset(55,82)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE2,7)
HDKO S 0 A_PlaySoundEx("Ryu_L/hadoken","SoundSlot6")
HDKO S 0 A_PlaySoundEx("weapon/heattackle","Weapon")
HDKO S 0 A_GiveInventory("RyuMainSuper_CI",1)
HDKO S 0 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO S 2 Offset(1,31)A_GiveInventory("RyuL_Flag",1)
HDKO U 0 ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE2,7)
HDKO U 2 Offset(1,31)A_TakeInventory("RyuMainFlagX",1)
HDKO S 2 Offset(1,31)ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO U 0 A_TakeInventory("BasicArmor",999)
HDKO U 0 A_TakeInventory("BossBasicArmorAmount",999)
HDKO U 2 Offset(1,31)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE2,7)
HDKO S 2 Offset(1,31)ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO U 2 Offset(1,31)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE2,7)
HDKO S 0 ACS_ExecuteAlways(CORE_ACS_191,0,APROP_Speed,1,1)
HDKO S 2 Offset(20,52)ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO U 2 Offset(20,52)ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE2,7)
HDKO S 12 Offset(20,52)ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
Goto Ready1

AltFire:
HDKO B 0 A_JumpIfInventory("SpawningMisfire",1,"SpawnProtect")
HDKO B 0 A_JumpIfInventory("RyuSuperFlag",1,"ShinShoryu")
HDKO B 0 A_JumpIfInventory("RyuMainFlag",1,"ShakuCheck")
HDKO B 0 A_GiveInventory("RyuAltFlag",1)
HDKO B 0 A_Refire(1)
HDKO B 0 A_TakeInventory("RyuL_Flag",9)
HDKO B 3 A_WeaponReady(9)
HDKO B 0 A_GiveInventory("RyuL_PoundCake_P",1)
HDKO B 1 A_WeaponReady(9)
goto Tatsu
TatsuQuiet:
HDKO B 0 A_TakeInventory("RyuL_Flag",9)
HDKO B 0 A_Refire(1)
HDKO B 3
HDKO B 1
goto Tatsu
Tatsu:
HDKO O 0 A_GiveInventory("MegaSlide",1)
HDKO O 0 A_PlaySoundEx("weapon/topspin","Weapon")
HDKO O 0 SetPlayerProperty(0,1,0)
HDKO O 0 A_TakeInventory("UnSolid",1)
HDKO O 0 A_ChangeFlag("THRUSPECIES",1)
HDKO O 0 A_ChangeFlag("SOLID",0)
HDKO O 2 A_GiveInventory("RyuL_AltfireThrust_P",1)
HDKO P 1 A_WeaponReady(8)
HDKO P 0 A_GiveInventory("RyuL_HurricaneH_P",1)
HDKO P 1 OffSet(-37,32)A_WeaponReady(9)
HDKO P 1 OffSet(-74,32)A_WeaponReady(9)
HDKO P 0 A_GiveInventory("RyuL_HurricaneH_P",1)
HDKO P 1 OffSet(-111,32)A_WeaponReady(9)
HDKO P 1 OffSet(-148,32)A_WeaponReady(9)
HDKO P 0 A_GiveInventory("RyuL_HurricaneH_P",1)
HDKO P 1 OffSet(296,32)A_WeaponReady(9)
HDKO P 1 OffSet(259,32)A_WeaponReady(9)
HDKO P 0 A_GiveInventory("RyuL_HurricaneH_P",1)
HDKO P 1 OffSet(222,32)A_WeaponReady(9)
HDKO P 1 OffSet(185,32)A_WeaponReady(9)
HDKO P 0 A_GiveInventory("RyuL_HurricaneH_P",1)
HDKO P 1 OffSet(148,32)A_WeaponReady(9)
HDKO P 1 OffSet(111,32)A_WeaponReady(9)
HDKO P 0 A_GiveInventory("RyuL_HurricaneH_P",1)
HDKO P 1 OffSet(74,32)A_WeaponReady(9)
HDKO P 1 OffSet(37,32)A_WeaponReady(9)
HDKO P 0 A_GiveInventory("RyuL_HurricaneH_P",1)
HDKO P 1 A_WeaponReady(8)
HDKO O 5 SetPlayerProperty(0,0,0)
HDKO B 0 A_GiveInventory("RyuL_Flag",1)
HDKO B 0 A_ChangeFlag("THRUSPECIES",0)
HDKO B 0 A_SpawnItemEx("ReSolidifier")
HDKO B 8
HDKO B 0 A_Refire("TatsuQuiet")
HDKO A 0 A_TakeInventory("MegaSlide",1)
HDKO A 0 A_TakeInventory("RyuAltFlag",1)
Goto Ready1

ShinTatCheck: 
HDKO A 0 A_JumpIfInventory("SuperComboGauge",7,"EXAltfire")
goto Tatsu
EXAltFire:
HDKO O 0 A_PlaySoundEx("Ryu_L/EXMove","Body")
HDKO O 0 A_PlaySoundEx("Ryu_L/shintatsu","SoundSlot6")
HDKO O 0 A_PlaySoundEx("weapon/topspin","Weapon")
HDKO O 0 A_GiveInventory("NoJumpCancel",1)
HDKO O 0 A_GiveInventory("InvulnerableOn_Pickup",1)
HDKO O 0 SetPlayerProperty(0,1,0)
HDKO O 0 SetPlayerProperty(0,1,3)
HDKO Q 0 A_SpawnItemEx("RyuL_TrueHurricaneFX",0,0,0,0,0,0,0,1)
HDKO O 0 A_TakeInventory("SuperComboGauge",7)
HDKO O 0 A_TakeInventory("UnSolid",1)
HDKO O 0 A_ChangeFlag("THRUSPECIES",1)
HDKO O 0 A_ChangeFlag("SOLID",0)
HDKO Q 0 ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO Q 2 A_ChangeVelocity(0,0,momz*0.5+12,3)

HDKO P 1 OffSet(-37,32)A_FireCustomMissile("RyuL_TrueHurricane",0,0,0,-8)
HDKO P 1 OffSet(-74,32)A_JumpIfInventory("PowerRage_ST",1,1)
HDKO R 0 ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO R 1 OffSet(-111,32)A_FireCustomMissile("RyuL_TrueHurricane",0,0,0,-8)
HDKO R 1 OffSet(-148,32)A_JumpIfInventory("PowerRage_ST",1,1)
HDKO P 0 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO P 1 OffSet(296,32)A_FireCustomMissile("RyuL_TrueHurricane",0,0,0,-8)
HDKO P 1 OffSet(259,32)A_JumpIfInventory("PowerRage_ST",1,1)
HDKO R 0 ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO R 1 OffSet(222,32)A_FireCustomMissile("RyuL_TrueHurricane",0,0,0,-8)
HDKO R 1 OffSet(185,32)A_JumpIfInventory("PowerRage_ST",1,1)
HDKO P 0 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO P 1 OffSet(148,32)A_FireCustomMissile("RyuL_TrueHurricane",0,0,0,-8)
HDKO P 1 OffSet(111,32)A_JumpIfInventory("PowerRage_ST",1,1)
HDKO R 0 ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO R 1 OffSet(74,32)A_FireCustomMissile("RyuL_TrueHurricane",0,0,0,-8)
HDKO R 1 OffSet(37,32)A_JumpIfInventory("PowerRage_ST",1,1)
HDKO O 0 A_WeaponReady(14)
HDKO O 0 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO O 2 SetPlayerProperty(0,0,3)
HDKO Q 0 ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE1,6)
HDKO Q 2 A_GiveInventory("RyuL_Flag",1)
HDKO O 2 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
HDKO O 0 SetPlayerProperty(0,0,0)
HDKO O 0 A_ChangeFlag("THRUSPECIES",0)
HDKO O 0 A_SpawnItemEx("ReSolidifier")
HDKO O 0 A_GiveInventory("InvulnerableOff_Pickup",1)
HDKO O 2
HDKO B 2 A_TakeInventory("NoJumpCancel",1)
HDKO B 0 A_TakeInventory("MegaSlide",1)
HDKO B 0 A_TakeInventory("RyuAltFlag",1)
HDKO B 0 A_TakeInventory("RyuMainFlag",1)
Goto Ready1

Shoryu:
HDKO W 0 A_GiveInventory("MegaShoryu",1)
HDKO W 0 A_GiveInventory("NoJumpCancel",1)
HDKO W 0 A_Refire(1)
HDKO W 0 A_PlaySoundEx("Ryu_L/shoryuken","SoundSlot6")
HDKO W 0 A_PlaySoundEx("weapon/SlashDClaw","Weapon")
HDKO W 0 SetPlayerProperty(0,1,0)
HDKO W 1 Offset(-6,111)
HDKO V 1 Offset(-12,80)
HDKO V 0 A_GiveInventory("LadderDisable",1)
HDKO V 0 A_ChangeVelocity(12,0,16,3)
HDKO V 0 A_SpawnItemEx("RyuL_ShoryukenWarper",0,0,0,0,0,0,0,1)
HDKO V 0 A_SpawnItemEx("RyuL_ShoryukenWarper",0,0,0,0,0,0,180,1)
HDKO VV 1 Offset(-12,80)A_GiveInventory("RyuL_ShoryukenH_P",1)
HDKO VVVVVVVVVV 1 Offset(1,31)A_GiveInventory("RyuL_ShoryukenH_P",1)
HDKO W 2 Offset(1,31)
HDKO W 2 Offset(38,43)
TNT1 A 4
TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
TNT1 A 0 A_SpawnItemEx("ReSolidifier")
TNT1 A 2 A_GiveInventory("LadderEnable",1)
TNT1 A 0 A_TakeInventory("NoJumpCancel",1)
TNT1 A 0 A_TakeInventory("MegaShoryu",1)
TNT1 A 0 A_TakeInventory("MegaSlide",1)
TNT1 A 2 A_WeaponReady(14)
TNT1 A 0 SetPlayerProperty(0,0,0)
goto Recovery
Recovery:
TNT1 A 0 A_JumpIf(z-floorz==0,"Recovered")
TNT1 A 0 A_JumpIf(momz==0,"Recovery_Z")
TNT1 A 0 A_JumpIfInventory("RyuRecoverFlag",110,"Recovered")
TNT1 A 1 A_GiveInventory("RyuRecoverFlag",1)
loop
Recovery_Z:
TNT1 A 1
TNT1 A 0 A_JumpIf(z-floorz==0,"Recovered")
TNT1 A 0 A_JumpIf(momz==0,"Recovered")
goto Recovery+2

Recovered:
HDKO O 1 A_GiveInventory("RyuL_Flag",1)
HDKO O 0 A_TakeInventory("RyuMainFlag",1)
HDKO O 0 A_TakeInventory("RyuAltFlag",1)
HDKO O 0 A_TakeInventory("RyuRecoverFlag",999)
HDKO OOOO 2 A_WeaponReady(8)
goto Ready1

ShinShoryuFail_1:
HDKO W 0 A_JumpIfInventory("RyuShinShoryuFlag",1,"ShinShoryu2")
HDKO W 0 A_GiveInventory("SelfTotallyUnfrozen",1)
HDKO W 0 A_ChangeFlag("NOGRAVITY",0)
HDKO W 0 A_GiveInventory("InvulnerableOff_Pickup",1)
goto Shoryu

ShinShoryuFail_2:
HDKO W 0 A_JumpIfInventory("RyuShinShoryuFlag",1,"ShinShoryu3")
HDKO W 0 A_GiveInventory("SelfTotallyUnfrozen",1)
HDKO W 0 A_ChangeFlag("NOGRAVITY",0)
HDKO W 0 A_GiveInventory("InvulnerableOff_Pickup",1)
goto Shoryu

ShinShoryu:
HDKO X 0 A_GiveInventory("RyuAltFlagX",1)
HDKO X 0 A_Refire(1)
HDKO X 0 A_GiveInventory("NoJumpCancel",1)
HDKO X 0 A_GiveInventory("SelfTotallyFrozen",1)
HDKO X 0 A_GiveInventory("InvulnerableOn_Pickup",1)
HDKO X 0 A_PlaySoundEx("Ryu_L/shinshor1","SoundSlot6")
HDKO X 0 A_TakeInventory("SuperComboGauge",999)
HDKO X 0 A_TakeInventory("RyuL_Flag",9)
HDKO X 0 A_TakeInventory("UnSolid",1)
HDKO X 0 A_ChangeFlag("THRUSPECIES",1)
HDKO X 0 A_ChangeFlag("NOGRAVITY",1)
HDKO X 0 A_Stop
HDKO X 1
HDKO X 2 A_TakeInventory("RyuShinShoryuFlag",1)
HDKO Y 1 A_SpawnItemEx("ShinShoryukenStart",24,-12,24,30,0,0,9)
HDKO Y 2 A_SpawnItemEx("ShinShoryukenStart",24,-12,24,30,0,0,9)
HDKO Y 0 A_PlaySoundEx("Ryu_L/shinhit","Weapon")
HDKO Y 0 A_SpawnItemEx("ShinShoryu2",24,-12,24,momx,0,0,0,9)
HDKO YY 1 A_JumpIfInventory("RyuShinShoryuFlag",1,"ShinShoryu2")
goto ShinShoryuFail_1
ShinShoryu2:
HDKO Y 2
HDKO Y 2 A_GiveInventory("VivifyDelay5",1)
HDKO Y 30 A_TakeInventory("RyuShinShoryuFlag",9)
HDKO W 0 A_PlaySoundEx("Ryu_L/shinshor2","SoundSlot6")
HDKO W 3 Offset(-6,111)
HDKO V 1 Offset(-12,80)A_SpawnItemEx("ShinShoryukenStart",24,-12,24,30,0,0,9)
HDKO V 2 Offset(-12,80)A_SpawnItemEx("ShinShoryukenStart",24,-12,24,30,0,0,9)
HDKO V 0 A_PlaySoundEx("Ryu_L/shinhit","Weapon")
HDKO V 0 A_SpawnItemEx("ShinShoryu2",24,20,32,momx,0,0,0,9)
HDKO VV 1 A_JumpIfInventory("RyuShinShoryuFlag",1,"ShinShoryu3")
goto ShinShoryuFail_2
ShinShoryu3:
HDKO V 2 Offset(-12,80)A_GiveInventory("VivifyDelay5",1)
HDKO V 2 A_GiveInventory("LadderDisable",1)
HDKO V 24 Offset(-12,80)A_TakeInventory("RyuShinShoryuFlag",9)
HDKO V 0 A_PlaySoundEx("Ryu_L/shoryuken","SoundSlot6")
HDKO V 0 A_ChangeFlag("NOGRAVITY",1)
HDKO V 0 A_SpawnItemEx("ShinShoryukenFinalHit",24,-12,24,40,0,0,9)
HDKO V 0 A_ChangeVelocity(2,0,48,3)
HDKO V 2 Offset(-12,80)A_GiveInventory("MegaShoryu",1)
HDKO VVVV 2 Offset(1,31)
HDKO V 0 //A_Stop
HDKO V 0 A_PlaySoundEx("Ryu_L/shinhit2","Weapon")
HDKO V 0 A_PlaySoundEx("Ryu_L/knockout","Body")
HDKO V 0 A_SpawnItemEx("ShinShoryu2",28,20,48,momx,0,0,0,9)
HDKO V 18 Offset(1,31)
HDKO W 0 A_ChangeFlag("THRUSPECIES",0)
HDKO W 0 A_SpawnItemEx("ReSolidifier")
HDKO W 0 A_ChangeFlag("NOGRAVITY",0)
HDKO W 2 Offset(1,31)
HDKO W 2 Offset(38,43)A_GiveInventory("LadderEnable",1)
TNT1 A 2 A_GiveInventory("InvulnerableOff_Pickup",1)
TNT1 A 2 A_TakeInventory("NoJumpCancel",1)
TNT1 A 2 A_TakeInventory("RyuShinShoryuFlag",9)
TNT1 A 2 A_TakeInventory("MegaShoryu",9)
TNT1 A 0 A_GiveInventory("SelfTotallyUnfrozen",1)
goto Recovery

Flash:
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 1 A_JumpIfInventory("SuperComboGauge",28,"Flash_LetsGo")
loop
Flash_LetsGo:
TNT1 A 0 A_PlaySoundEx("Ryu_L/letsgo","SoundSlot7")
Flash_SuperReady:
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 1 A_JumpIfInventory("RyuSuperFlag",1,"Flash_Drain")
TNT1 A 0 A_JumpIfInventory("SuperComboGauge",28,"Flash_SuperReady")
goto Flash_Drain+5
Flash_Drain:
TNT1 A 0 A_GiveInventory("RyuL_SuperFlagPower",1)
TNT1 A 2 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 3 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 0 A_TakeInventory("SuperComboGauge",1)
TNT1 A 0 A_JumpIfInventory("SuperComboGauge",1,"Flash_Drain")
TNT1 A 0 A_TakeInventory("RyuSuperFlag",1)
goto Flash
}
}

actor SuperComboGauge : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor DrRightAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 490
+INVENTORY.IGNORESKILL
}

actor RyuL_Flag : OnceC{}

actor MegaShoryu : OnceC{}

actor RyuMainFlag : OnceC{}
actor RyuAltFlag : OnceC{}
actor RyuMainFlagX : OnceC{}
actor RyuAltFlagX : OnceC{}


actor RyuRecoverFlag : Inventory
{
inventory.amount 1
inventory.maxamount 255
}

actor RyuSuperFlag : OnceC{}
actor RyuShinShoryuFlag : OnceC{}

actor RyuL_SuperFlagPower : PowerSpeed
{
+POWERSPEED.NOTRAIL
powerup.duration 8
speed 1.25
}


actor RyuL_ArmorPickup : BasicArmorPickup
{
Armor.Savepercent 50.001
Armor.Saveamount 40
}

actor RyuShinShoryuFlag_P : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("RyuShinShoryuFlag",1)
stop
}
}

actor RyuSuperItem : BasicClassItem
{
inventory.pickupmessage "Always pick Denjin Hadoken unless you're a scrub"
inventory.icon "RyuIcon"
Tag ""
states
{
Spawn:
HDKO Z 1
loop
Use:
TNT1 A 0 A_JumpIfInventory("RyuSuperFlag",1,"No")
TNT1 A 0 A_JumpIfInventory("SuperComboGauge",28,"Success")
fail
Success:
TNT1 A 0 A_PlaySoundEx("Ryu_L/superact","Item")
TNT1 A 0 A_GiveInventory("RyuSuperFlag",1)
fail
}
}


actor RyuL_PoundCake_P : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_Jump(3,"P_Snd2")
goto P_Snd1
P_Snd1:
TNT1 A 0 A_PlaySoundEx("Ryu_L/tatsumaki","SoundSlot6")
stop
P_Snd2:
TNT1 A 0 A_PlaySoundEx("Ryu_L/poundcake","SoundSlot6")
stop
}
}

actor RyuL_AltfireThrust_P : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DrRightAmmo",70,"PickupCheck")
goto WeakCheck
PickupCheck:
TNT1 A 0 A_GiveInventory("RyuL_StaminaTakeFlag",1)
TNT1 A 0 A_JumpIfInventory("IsUnderWater",1,"P_Water")
goto PickupNormal
PickupNormal:
TNT1 A 0 A_ChangeVelocity(37,0,momz/2+6,3)
stop
P_Water:
TNT1 A 0 A_JumpIf(momz<0,"P_WaterD")
goto P_WaterU
P_WaterU:
TNT1 A 0 A_ChangeVelocity(37,0,momz/3+6,3)
stop
P_WaterD:
TNT1 A 0 A_ChangeVelocity(37,0,momz+6,3)
stop

WeakCheck:
TNT1 A 0 A_JumpIfInventory("IsUnderWater",1,"W_Water")
goto WeakNormal
WeakNormal:
TNT1 A 0 A_ChangeVelocity(18,0,momz/2+3,3)
stop
W_Water:
TNT1 A 0 A_JumpIf(momz<0,"W_WaterD")
goto W_WaterU
W_WaterU:
TNT1 A 0 A_ChangeVelocity(18,0,momz/3+3,3)
stop
W_WaterD:
TNT1 A 0 A_ChangeVelocity(18,0,momz+3,3)
stop

}
}

actor RyuL_HurricaneH_P : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("RyuL_StaminaUse_P",1)
TNT1 A 0 A_JumpIfInventory("RyuL_StaminaTakeFlag",1,"Pickup2")
goto PickupW
Pickup2:
TNT1 A 0 A_SpawnItemEx("RyuL_HurricaneRadius",0,0,28,momx,momy,momz,0,9,0)
TNT1 A 0 A_JumpIfInventory("PowerSpread_ST",1,"Pickup2_S")
stop
Pickup2_S:
TNT1 AA 0 A_SpawnItemEx("RyuL_HurricaneRadius",0,0,28,momx,momy,momz,0,9,0)
stop
PickupW:
TNT1 A 0 A_SpawnItemEx("RyuL_HurricaneRadius3",0,0,28,momx,momy,momz,0,9,0)
TNT1 A 0 A_JumpIfInventory("PowerSpread_ST",1,"PickupW_S")
stop
PickupW_S:
TNT1 AA 0 A_SpawnItemEx("RyuL_HurricaneRadius",0,0,28,momx,momy,momz,0,9,0)
stop
}
}

actor RyuL_ShoryukenH_P : CustomInventory
{
States
{
Pickup://Sheng Long
//A_SpawnItemEx("RyuL_ShoryukenHitbox",0,4,32,24)
//TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenHitbox",0,0,28,momx*4.5,momy*4.5,0,0,9)
TNT1 A 0 A_JumpIfInventory("PowerSpread_ST",1,"PickupR")
TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenRadius",0,0,28,momx*4.5,momy*4.5,0,0,9)
stop
PickupR:
//TNT1 AA 0 A_SpawnItemEx("RyuL_ShoryukenRadius",0,0,28,momx*4.5,momy*4.5,0,0,9)
//TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenHitbox",0,0,28,momx*4.5,momy*4.5,0,20,9)
//TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenHitbox",0,0,28,momx*4.5,momy*4.5,0,-20,9)
TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenRadiusA",0,0,28,momx*4.5,momy*4.5,0,0,9)
TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenRadiusB",0,0,28,momx*4.5,momy*4.5,0,20,9)
TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenRadiusC",0,0,28,momx*4.5,momy*4.5,0,-20,9)
stop
}
}

actor RyuL_StaminaTakeFlag : Powerup
{
Powerup.Duration 20
}

actor RyuL_StaminaUse_P : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("RyuL_StaminaDelay",1)
TNT1 A 0 A_JumpIfInventory("RyuL_StaminaTakeFlag",1,"Pickup1")
goto No
Pickup1:
TNT1 A 0 A_JumpIfInventory("DrRightAmmo",490,"Pickup2")
TNT1 A 0 A_TakeInventory("DrRightAmmo",10)
stop
Pickup2:
TNT1 A 0 A_SpawnItemEx("RyuL_StaminaUse_H",0,0,24)
stop
No:
TNT1 A 0
stop
}
}
actor RyuL_StaminaDelay : Powerup
{
Powerup.Duration 88
}
actor RyuL_StaminaUse_H : BasicHelper
{
States
{
Spawn:
TNT1 A 1
TNT1 A 5 A_TakeFromTarget("DrRightAmmo",10)
Spawn2:
TNT1 A 0 A_JumpIfInTargetInventory("RyuL_StaminaDelay",1,2)
TNT1 A 0 A_GiveToTarget("DrRightAmmo",1)
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("RyuL_StaminaDelay",1,"Spawn2")
TNT1 A 0 A_JumpIfInTargetInventory("DrRightAmmo",490,"Death")
TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(C_CHECK_TARGET_OOC)==0,"Death")
loop
}
}



actor RyuL_SelectionFX : BasicExplosion
{
ReactionTime 16
Scale 2.5
States
{
Spawn:
HDOK Z 1
TNT1 A 1 A_CountDown
loop
}
}

actor RyuL_HadokenShot
{
translation "202:202=192:192","199:199=198:198"
PROJECTILE
damagetype "LightRyu4"
Obituary "$OB_RYUL_M1"
Renderstyle Translucent
Alpha 0.9
Damage (16)
radius 12
height 12
Speed 36
scale 2.5
States
{
Spawn:
HDOK OP 4
loop
Death:
XDeath:
TNT1 A 2 A_SpawnItemEx("RyuL_HadokenShotFX",0,0,0,0,0,0,0,1)
stop
Crash:
TNT1 A 2
stop
}
}

actor RyuL_HadokenShotB : RyuL_HadokenShot{translation "202:202=205:205","199:199=74:74"}
actor RyuL_HadokenShotR : RyuL_HadokenShot{translation "202:202=171:171","199:199=41:41"}
actor RyuL_HadokenShotO : RyuL_HadokenShot{translation "202:202=104:104","199:199=128:128"}
actor RyuL_HadokenShotP : RyuL_HadokenShot{translation "202:202=229:229","199:199=232:232"}

actor RyuL_HadokenShotFX : BasicClientSide
{
Scale 3.0
States
{
Spawn:
Y_4D PQRSTYVWXY 1
stop
}
}

actor LightRyuFireProtect : PowerProtection
{
Powerup.Duration 3
DamageFactor "LightRyuFire", 0.2
}

actor RyuL_HadokenEXShot
{
translation "202:202=175:175","199:199=190:190"
PROJECTILE
damagetype "LightRyuFire"
Obituary "$OB_RYUL_M2"
+THRUACTORS
+THRUGHOST
Renderstyle Translucent
Alpha 0.9
Damage (0)
radius 4
height 4
Speed 36
scale 2.75
States
{
Spawn:
HDOK Q 0
HDOK Q 0 A_Explode(16,42,0,0,43)
HDOK Q 1 A_SpawnItemEx("RyuL_HadokenEXShotFX",random(-4,4),random(-4,4),random(-8,0),0,0,0,0,1)
HDOK Q 0 A_Explode(16,42,0,0,43)
HDOK Q 1 A_SpawnItemEx("RyuL_HadokenEXShotFX",random(-4,4),random(-4,4),random(-8,0),0,0,0,0,1)
HDOK R 0 A_Explode(16,42,0,0,43)
HDOK R 1 A_SpawnItemEx("RyuL_HadokenEXShotFX",random(-4,4),random(-4,4),random(-8,0),0,0,0,0,1)
HDOK R 0 A_Explode(16,42,0,0,43)
HDOK R 1 A_SpawnItemEx("RyuL_HadokenEXShotFX",random(-4,4),random(-4,4),random(-8,0),0,0,0,0,1)
loop
Death:
XDeath:
TNT1 A 0 A_Explode(2,42,0,0,43)
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
TNT1 A 2 A_SpawnItemEx("RyuL_HadokenShotFX",0,0,0,0,0,0,0,1)
stop
Crash:
TNT1 A 2
stop
}
}

actor RyuL_HadokenEXShotB : RyuL_HadokenEXShot{translation "202:202=205:205","199:199=74:74"}
actor RyuL_HadokenEXShotR : RyuL_HadokenEXShot{translation "202:202=171:171","199:199=41:41"}
actor RyuL_HadokenEXShotO : RyuL_HadokenEXShot{translation "202:202=104:104","199:199=128:128"}
actor RyuL_HadokenEXShotP : RyuL_HadokenEXShot{translation "202:202=229:229","199:199=232:232"}

actor RyuL_HadokenEXShotFX : BasicClientSide
{
//Translation "202:202=175:175","199:199=190:190"
Renderstyle Translucent
Alpha 0.9
Scale 1
States
{
Spawn:
HDOK STUV 3 A_FadeOut(0.1)
loop
}
}
actor LightRyuFireBurn : BasicExplosion
{
damagetype "WilyFireB2"
Obituary "$OB_RYUL_M2X"
States
{
Spawn:
TNT1 A 0
TNT1 A 2 A_Explode(2,15,0,0,15)
stop
}
}

actor RyuL_HadokenSuperCharge_P1 : CustomInventory
{
States
{
Pickup:
TNT1 A 0 ACS_ExecuteAlways(998,0,DYE_LIGHTRYU,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperCharge",0,0,28,0,0,0,0,1)
stop
}
}

actor RyuL_HadokenSuperCharge_P2 : CustomInventory
{
States
{
Pickup:
TNT1 A 0 ACS_ExecuteAlways(998,0,DYE_LIGHTCHARGE2,7)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperCharge",0,0,28,0,0,0,0,1)
stop
}
}

actor RyuL_HadokenSuperCharge : BasicHelper
{
States
{
Spawn:
TNT1 A 0
TNT1 A 2 A_SpawnItemEx("RyuL_HadokenSuperCharge1",0,0,0,0,0,0,0,1)
stop
}
}

actor RyuL_HadokenSuperCharge1 : BasicClientSide
{
var int user_A;
reactiontime 12
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_A",random(0,30))
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperCharge2",80,0,random(-10,10),140,0,0,user_A+random(-15,15),33,224)

TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperCharge2",60,0,random(0,20),120,0,40,user_A+random(-25,25),33,224)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperCharge2",60,0,random(-20,0),120,0,-40,user_A+random(-25,25),33,224)

TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperCharge2",40,0,random(10,30),100,0,60,user_A+random(-25,25),33,224)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperCharge2",40,0,random(-30,10),100,0,-60,user_A+random(-25,25),33,224)

TNT1 A 0 A_SetUserVar("user_A",user_A+30)
TNT1 A 0 A_Countdown
goto Spawn+2
Death:
TNT1 A 0
stop
}
}

actor RyuL_HadokenSuperCharge2 : BasicClientSide
{
+MISSILE
Speed 20
States
{
Spawn:
TNT1 A 0
HDOK L 1 A_ScaleVelocity(-0.1)
HDOK LLMMMNNN 1 A_ScaleVelocity(1.1)
stop
}
}

actor RyuL_HadokenSuperShot
{
var int user_D;
translation "202:202=192:192","199:199=198:198"
PROJECTILE
damagetype "ClassPainLess2"
Obituary "$OB_RYUL_M3"
+THRUACTORS
+THRUGHOST
+DONTBLAST
renderstyle translucent
alpha 0.9
ReactionTime 10
Damage (0)
radius 4
height 4
Speed 38
scale 3.5
States
{
Spawn:
HDOK I 0
HDOK I 0 ACS_NamedExecuteAlways("cbm_missileyank",0,96)
HDOK I 0 A_Explode(2,96,0,0,97)
HDOK I 0 A_SetUserVar("user_D",user_D+1)
HDOK III 1 ACS_NamedExecuteAlways("cbm_missileyank",0,96)
HDOK J 0 A_SetTranslucent(0.9)
HDOK J 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX2",0,0,0,0,0,0,0,1)
HDOK J 0 A_Explode(2,96,0,0,97)
HDOK J 0 A_SetUserVar("user_D",user_D+1)
HDOK JJJ 1 ACS_NamedExecuteAlways("cbm_missileyank",0,96)
HDOK K 0 A_SetTranslucent(1.0)
HDOK K 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX2",0,0,0,0,0,0,0,1)
HDOK K 0 A_Explode(2,96,0,0,97)
HDOK K 0 A_SetUserVar("user_D",user_D+1)
HDOK KKK 1 ACS_NamedExecuteAlways("cbm_missileyank",0,96)
HDOK I 0 A_SetTranslucent(0.8)
HDOK I 0 A_CountDown
HDOK I 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX2",0,0,0,0,0,0,0,1)
goto Spawn+2
Death:
TNT1 A 0 A_Explode(70-(user_D*0.334),196,0,0,96)
TNT1 A 2 A_SpawnItemEx("RyuL_HadokenSuperShotEnd",0,0,0,0,0,0,0,1)
stop
}
}


actor RyuL_HadokenSuperShotEnd : BasicExplosion
{
damagetype "ClassPainStop"
Obituary "$OB_RYUL_M3"
Scale 6.0
States
{
Spawn:
Y_4D P 0
Y_4D P 0 A_Explode(2,128,0,0,129)
Y_4D PQRSTYVWXY 1
stop
}
}

actor RyuL_HadokenSuperShotFX : BasicClientSide
{
States
{
Spawn:
GIGO UV 3
GIGO W 2 A_ScaleVelocity(1.15)
GIGO W 1 A_Fadeout(0.2)
goto Spawn+2
}
}

actor RyuL_HadokenSuperShotFX2 : BasicClientSide
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,4,12,-1,1,3,0,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,-4,12,-1,-1,3,0,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,12,4,-1,3,1,0,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,12,-4,-1,3,-1,0,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,4,-12,-1,1,-3,0,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,-4,-12,-1,-1,-3,0,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,-12,4,-1,-3,1,0,1)
TNT1 A 0 A_SpawnItemEx("RyuL_HadokenSuperShotFX",0,-12,-4,-1,-3,-1,0,1)
TNT1 A 1
stop
}
}


actor RyuL_HadokenSuperShotB : RyuL_HadokenSuperShot{translation "202:202=205:205","199:199=74:74"}
actor RyuL_HadokenSuperShotR : RyuL_HadokenSuperShot{translation "202:202=171:171","199:199=41:41"}
actor RyuL_HadokenSuperShotO : RyuL_HadokenSuperShot{translation "202:202=104:104","199:199=128:128"}
actor RyuL_HadokenSuperShotP : RyuL_HadokenSuperShot{translation "202:202=229:229","199:199=232:232"}

actor RyuL_HurricaneRadius : BasicExplosion
{
Damagetype "LightRyu1"
Obituary "$OB_RYUL_A1"
States
{
Spawn:
TNT1 A 0
TNT1 A 2 A_Explode(ACS_NamedExecuteWithResult("cbm_getplayervelocity",7,0)+1,96,0,0,10)
stop
}
}

actor RyuL_HurricaneRadius3 : RyuL_HurricaneRadius
{
States
{
Spawn:
TNT1 A 0
TNT1 A 2 A_Explode(ACS_NamedExecuteWithResult("cbm_getplayervelocity",3,0)+1,96,0,0,10)
stop
}
}

actor RyuL_TrueHurricaneFX : BasicExplosion
{
Scale 2.5
States
{
Spawn:
HDOK ABCABC 3
stop
}
}

actor RyuL_TrueHurricane : BasicExplosion
{
Damagetype "ClassPainLess2"
Obituary "$OB_RYUL_A2"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_ExecuteAlways(247,0,14,320)
TNT1 A 4 A_Explode(9,160,0,0,32)
stop
}
}

actor RyuL_ShoryukenWarper : ShieldWarper_CBM
{
+NOTIMEFREEZE
ReactionTime 12
States
{
Spawn:
TNT1 A 0
Goto Shield
Shield:
TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(C_CHECK_TARGET_OOC)==0,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("PowerRage_ST",1,"ShieldR")
TNT1 A 0 A_Warp(AAPTR_TARGET,64,0,16,15,28)
TNT1 A 0 A_SpawnItemEx("RyuL_ShoryukenWarperFX",0,0,0,0,0,1,0,1)
TNT1 A 1 A_CountDown
loop
ShieldR:
TNT1 A 0 A_Warp(AAPTR_TARGET,64,0,16,30,28)
goto Shield+3
Death:
TNT1 A 1
stop
}
}

actor RyuL_ShoryukenWarperFX : BasicClientSide
{
+MISSILE
+DONTBLAST
Speed 40
States
{
Spawn:
TNT1 A 0
TMFX S 0 A_SetScale(1.0,2.0)
TMFX SSSSSSSSSS 1 A_SetScale(ScaleX - 0.1,ScaleY - 0.1)
stop
}
}

actor RyuL_ShoryukenRadius : BasicExplosion
{
damagetype "LightRyuShoryu"//LightRyu1
Obituary "$OB_RYUL_A3"
States
{
Spawn:
TNT1 A 0
TNT1 A 3 A_Explode(13,56,0,0,57)
stop
}
}

actor RyuL_ShoryukenRadiusA : RyuL_ShoryukenRadius { damagetype "LightRyuShoryuA" }
actor RyuL_ShoryukenRadiusB : RyuL_ShoryukenRadius { damagetype "LightRyuShoryuB" }
actor RyuL_ShoryukenRadiusC : RyuL_ShoryukenRadius { damagetype "LightRyuShoryuC" }

actor RyuL_ShoryukenHitbox
{
PROJECTILE
damagetype "LightRyuShoryu"
Obituary "$OB_RYUL_A3"
+THRUACTORS
+DONTBLAST
+DONTREFLECT
ReactionTime 1
damage (0)
radius 4
height 4
Speed 60
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
wait
Death:
TNT1 A 0
TNT1 A 2 A_Explode(3,56,0,0,57)
//TMFX R 2
//YMFX Q 2
//TMFX RS 2
stop
}
}

actor RyuL_ShoryukenProtect : PowerProtection
{
Powerup.Duration 4
DamageFactor "LightRyuShoryu", 0.16
}
actor RyuL_ShoryukenAProtect : RyuL_ShoryukenProtect { DamageFactor "LightRyuShoryuA", 0.16 }
actor RyuL_ShoryukenBProtect : RyuL_ShoryukenProtect { DamageFactor "LightRyuShoryuB", 0.16 }
actor RyuL_ShoryukenCProtect : RyuL_ShoryukenProtect { DamageFactor "LightRyuShoryuC", 0.16 }

actor ShinShoryukenStart
{
PROJECTILE
damagetype "LightRyuStop"
Obituary "$OB_RYUL_A4X"
+THRUGHOST
+DONTBLAST
+DONTREFLECT
ReactionTime 1
Damage (0)
radius 12
height 12
Speed 30
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
wait
Death:
TNT1 A 3 A_Explode(2,56,0,0,57)
stop
}
}

actor LightRyuStopProtect : PowerProtection
{
Powerup.Duration 40
damagefactor "Normal", 0.0
damagefactor "LightRyuStop", 1.0
damagefactor "LightRyuShinShoryu", 1.0
}


actor ShinShoryu2 : BasicExplosion
{
scale 1.75
States
{
Spawn:
TNT1 A 0
X_1G NMLKJIHFE 1
stop
}
}

actor ShinShoryukenFinalHit
{
PROJECTILE
damagetype "LightRyuShinShoryu"
Obituary "$OB_RYUL_A4"
+THRUGHOST
+DONTBLAST
+DONTREFLECT
+PIERCEARMOR
ReactionTime 1
Damage (0)
radius 12
height 12
Speed 30
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
wait
Death:
TNT1 A 4 A_Explode(100,80,0,0,81)
stop
}
}



actor RyuMain_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenShot",0,0,0,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenShotB",0,0,0,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenShotR",0,0,0,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenShotO",0,0,0,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenShotP",0,0,0,0)
goto Done
}
}

actor RyuMainEX_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenEXShot",0,0,0,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenEXShotB",0,0,0,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenEXShotR",0,0,0,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenEXShotO",0,0,0,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenEXShotP",0,0,0,0)
goto Done
Done:
TNT1 A 0 A_TakeInventory("SuperComboGauge",7)
stop
}
}

actor RyuMainSuper_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenSuperShot",0,0,0,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenSuperShotB",0,0,0,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenSuperShotR",0,0,0,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenSuperShotO",0,0,0,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("RyuL_HadokenSuperShotP",0,0,0,0)
goto Done
}
}

