actor BubbleBombBoss : BaseMM8BDMWep_CBM
{
tag "$TAGC_VC"
Dropitem "BubbleBombWepCDropped"
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.AmmoUse2 28
Inventory.Pickupmessage "These telemarketers are going to make me miss Gino's sunscreen alarm!"
Obituary "$OB_VENUSTICKER"
weapon.ammotype "CrabbyBubbleAmmo"
weapon.ammotype2 "AwaDanceAmmo"
inventory.icon "VENUS_I1"
States
{
Spawn:
C_0V C 1
loop

Ready:
VENB A 0 ACS_ExecuteAlways(998,0,DYE_VENUS)
Goto Ready1
Ready1:
VENB A 0 A_JumpIfInventory("CrabbyBubbleAmmo",2,"Ready2")
VENB A 0 A_JumpIfInventory("AwaDanceAmmo",28,"Ready3")
VENB A 2 A_WeaponReady(12)
VENB A 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB A 0 A_GiveInventory("AwaDanceAmmo",1)
loop
Ready2:
VENB A 0 A_JumpIfInventory("AwaDanceAmmo",28,"Ready4")
VENB A 2 A_WeaponReady(8)
VENB A 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB A 0 A_GiveInventory("AwaDanceAmmo",1)
loop
Ready3:
VENB A 0 A_JumpIfInventory("CrabbyBubbleAmmo",2,"Ready4")
VENB A 2 A_WeaponReady(4)
VENB A 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB A 0 A_GiveInventory("AwaDanceAmmo",1)
loop
Ready4:
VENB A 2 A_WeaponReady
VENB A 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB A 0 A_GiveInventory("AwaDanceAmmo",1)
loop

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
VENB A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
VENB A 1 A_Raise
Loop

Fire:
VENB A 0 A_PlaySoundEx("W_Venus/Fire1","Weapon")
VENB B 0 A_GiveInventory("VivifyDelay5")
VENB B 2 A_GiveInventory("VenusMain1_CI")
VENB C 2 A_GiveInventory("AwaDanceAmmo",1)
VENB A 0 A_GiveInventory("AwaDanceAmmo",1)
Goto Ready1

Altfire:
VENB A 0 A_PlaySoundEx("weapon/GameBoyFire2","Weapon")
VENB A 0 A_GiveInventory("VenusAlt_CI")
VENB BBCCBBAAA 2
Goto Ready1
}
}

actor BubbleBomberBoss : BaseMM8BDMWep_CBM
{
tag "$TAGC_VC2"
Dropitem "BubbleBombWepCDropped"
Weapon.AmmoUse 10
Weapon.AmmoGive 28
Weapon.AmmoUse2 28
Inventory.Pickupmessage "Gino! You didn't tell me CrabCrab was a crab!"
Obituary "$OB_BUBBLEBOMB"
weapon.ammotype "CrabbyBubbleAmmo"
weapon.ammotype2 "AwaDanceAmmo"
inventory.icon "VENUS_I2"
States
{
Spawn:
C_0V C 1
loop

Ready:
VENB D 0 ACS_ExecuteAlways(998,0,DYE_VENUS)
goto Ready1
Ready1:
VENB D 0 A_JumpIfInventory("CrabbyBubbleAmmo",10,"Ready2")
VENB D 0 A_JumpIfInventory("AwaDanceAmmo",28,"Ready3")
VENB D 2 A_WeaponReady(12)
VENB D 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB D 0 A_GiveInventory("AwaDanceAmmo",1)
loop
Ready2:
VENB D 0 A_JumpIfInventory("AwaDanceAmmo",28,"Ready4")
VENB D 2 A_WeaponReady(8)
VENB D 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB D 0 A_GiveInventory("AwaDanceAmmo",1)
loop
Ready3:
VENB D 0 A_JumpIfInventory("CrabbyBubbleAmmo",10,"Ready4")
VENB D 2 A_WeaponReady(4)
VENB D 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB D 0 A_GiveInventory("AwaDanceAmmo",1)
loop
Ready4:
VENB D 2 A_WeaponReady
VENB D 0 A_GiveInventory("CrabbyBubbleAmmo",1)
VENB D 0 A_GiveInventory("AwaDanceAmmo",1)
loop

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
VENB D 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
VENB D 1 A_Raise
Loop

Fire:
VENB D 0 A_PlaySound("W_Venus/BubbleBomb",1,0.825)
VENB E 2 A_GiveInventory("VenusMain2_CI")
VENB EFFEEDDDD 2 A_GiveInventory("AwaDanceAmmo",1)
VENB D 0 A_GiveInventory("AwaDanceAmmo",1)
Goto Ready1

Altfire:
VENB D 0 A_PlaySoundEx("weapon/GameBoyFire2","Weapon")
VENB D 0 A_GiveInventory("VenusAlt_CI")
VENB EEFFEEDDD 2
Goto Ready1

}
}

actor CrabbyBubbleAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
+INVENTORY.IGNORESKILL
}

actor AwaDanceAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor VenusBlastJumpFlag : OnceC{}
actor VenusBlastJumpSpeed : PowerSpeed
{
Powerup.Duration 26
Speed 2.0
+POWERSPEED.NOTRAIL
}
actor VenusBlastJumpPower : Powerup
{
Powerup.Duration 20
}


actor VenusBubbleTicker
{
translation "204:204=68:68","195:195=70:70"
PROJECTILE
damagetype "BubbleBombB"
Obituary "$OB_VENUSTICKER"
ReactionTime 1
damage (4)
Radius 10
Height 10
speed 24
scale 2.5
States
{
Spawn://VENB GH 8 >> Accurate
//VENB GHGH 2
//VENB GHGHGHGHGHGH 2 ThrustThingZ(0,2,0,1)
VENB GH 4
VENB GGHHGGHHGGHH 2 ThrustThingZ(0,2,0,1)
VENB G 0 A_GiveInventory("OnceC",1)
VENB G 1 A_CountDown
wait
Crash:
XDeath:
VENB O 0 A_PlaySound("W_Venus/BubbPop",1,0.40)
VENB OPQ 1 A_Stop
stop
Death:
TNT1 A 0 A_JumpIfInventory("OnceC",1,"Crash")
TNT1 A 0 A_JumpIf(z-floorz>0, "Crash")
TNT1 A 1 A_SpawnItemEx("VenusBubbleMine",0,0,0,0,0,0,0,1)
stop
}
}

actor VenusBubbleTickerB : VenusBubbleTicker{Translation"204:204=205:205","195:195=74:74"}
actor VenusBubbleTickerR : VenusBubbleTicker{Translation"204:204=171:171","195:195=41:41"}
actor VenusBubbleTickerO : VenusBubbleTicker{Translation"204:204=104:104","195:195=128:128"}
actor VenusBubbleTickerP : VenusBubbleTicker{Translation"204:204=229:229","195:195=232:232"}

actor VenusBubbleMine
{
PROJECTILE
damagetype "BubbleBombB"
Obituary "$OB_VENUSMINE"
+DONTBLAST
+DONTREFLECT
//+THRUACTORS
+FLOORHUGGER
ReactionTime 1
damage (0)
radius 10
height 10
scale 2.5
states
{
Spawn:
//TNT1 A 1
//TNT1 A 1 A_ChangeFlag("FLOORHUGGER",1)
VENB G 0 //A_ChangeFlag("THRUACTORS",0)
//VENB GHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGHGH 2
//224
VENB G 0 //A_PlaySound
VENB GHGH 8
VENB GHGH 7
VENB GHGHGH 6
VENB GHGHGH 5
VENB GHGHGHGH 4
VENB GHGHGHGH 3
VENB GHGHGHGHGH 2
VENB GHGHGHGHGHGHGHGHGHGHGH 1

VENB G 1 A_CountDown
wait
Death:
VENB O 0 A_PlaySound("W_Venus/BubbPop",1,0.40)
VENB OPQ 1
stop
XDeath:
Crash:
TNT1 A 0 A_PlaySound("W_Venus/BubbBoom",1,0.75)
TNT1 A 0 A_Explode(6,96,0,0,16)
//VENB LMN 1
VMFX CB 2
YMFX K 2
XMFX K 2
stop
}
}

actor VenusBubbleBomb
{
Translation "202:202=210:210","204:204=68:68","195:195=70:70"
var int user_V;
PROJECTILE
damagetype "BubbleBomb"
Obituary "$OB_BUBBLEBOMB"
ReactionTime 5//1
damage (8)
Radius 16
Height 16
speed 40
scale 2.5
States
{
Spawn:
//VENB HG 10
//VENB J 13
//VENB JIJKJIJKJIJKJIJKJIJKJIJKJIJKJ 4
//Accurate ^^
VENB HG 2
VENB J 0 A_JumpIf(ReactionTime<=1,"Spawn2")
VENB J 0 A_SetScale(ScaleX+0.05)
VENB J 4 A_SetUserVar("user_V",user_V+1)
VENB I 0 A_SetScale(ScaleX+0.05)
VENB I 4 A_SetUserVar("user_V",user_V+1)
VENB J 0 A_SetScale(ScaleX+0.05)
VENB J 4 A_SetUserVar("user_V",user_V+1)
VENB K 0 A_SetScale(ScaleX+0.05)
VENB K 4 A_SetUserVar("user_V",user_V+1)
VENB J 0 A_CountDown
goto Spawn+2
Spawn2:
VENB J 0 A_GiveInventory("OnceC",1)
VENB J 1 A_CountDown
wait
Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_JumpIfInventory("OnceC",1,"DeathFX")
TNT1 A 0 A_PlaySoundEx("W_Venus/GB_Boom","Weapon")
TNT1 A 0 A_Explode(7+user_V,96+user_V,0,0,20+user_V*0.5)
TN_B EFGHIJKLMNOPQRS 1
stop
DeathFX:
VENB L 0 A_PlaySound("W_Venus/BubbPop",1,1.0)
VENB LMN 2
stop
}
}
actor VenusBubbleBombB : VenusBubbleBomb{Translation"202:202=205:205","204:204=205:205","195:195=74:74"}
actor VenusBubbleBombR : VenusBubbleBomb{Translation"202:202=171:171","204:204=171:171","195:195=41:41"}
actor VenusBubbleBombO : VenusBubbleBomb{Translation"202:202=104:104","204:204=104:104","195:195=128:128"}
actor VenusBubbleBombP : VenusBubbleBomb{Translation"202:202=229:229","204:204=229:229","195:195=232:232"}

actor VenusExplosiveJump_P : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("VenusBlastJumpPower",1)
TNT1 A 0 A_TakeInventory("VenusBlastJumpFlag",1)
TNT1 A 0 A_JumpIfInventory("FlightDisableFlag",1,"No")
TNT1 A 0 A_JumpIfInventory("CeilingFlag",1,"Gravityman")
TNT1 A 0 ThrustThingZ(0,80,0,0)
goto End
Gravityman:
TNT1 A 0 ThrustThingZ(0,80,1,0)
goto End
End:
TNT1 A 0 A_GiveInventory("VenusBlastJumpSpeed",1)
TNT1 A 0 A_GiveInventory("JumpCancler",1)
stop

No:
TNT1 A 0 ThrustThingZ(0,80,0,1)
stop
}
}

actor VenusExplosiveJump_H : BasicExplosion
{
damagetype "BubbleBomb"
Obituary "$OB_VENUSSHIELDJ"
Scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("W_Venus/BubbGet","Weapon")
TNT1 A 0 A_SetArg(4,88)//Original was 34 seconds
goto BoomWait
BoomWait:
VENB R 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",8)
VENB R 0 A_CheckFlag("DONTIDENTIFYTARGET","BoomWait_I",8)
VENB R 1 A_Warp(8,0,0,0,0,24)
VENB R 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",8)
VENB R 1 A_Warp(8,0,0,0,0,24)
VENB S 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",8)
VENB S 1 A_Warp(8,0,0,0,0,24)
VENB S 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",8)
VENB S 1 A_Warp(8,0,0,0,0,24)
VENB R 0 A_JumpIf(ACS_ExecuteWithResult(C_CHECK_TRACER_OOC)==0,"Death")
VENB R 0 A_CountDownArg(4)
loop
//CANTSEEK
BoomWait_I:
TNT1 A 0 A_JumpIfInventory("IsInGame",1,1,8)//Online sync
BoomWait_I2://Online sync
TNT1 AAAA 1 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",8)
TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(C_CHECK_TRACER_OOC)==0,"Death")
TNT1 A 0 A_CountDownArg(4)
TNT1 A 0 A_CheckFlag("DONTIDENTIFYTARGET","BoomWait_I2",8)
TNT1 A 0 A_JumpIfInventory("IsInGame",1,"BoomWait",8)//Online sync
goto BoomWait_I2//Online sync

BoomJump:
TNT1 A 0 A_Warp(8,0,0,16,0,24)
TNT1 A 0 A_PlaySound("W_Venus/JumpBoost",7,1.0)
TNT1 A 0 A_Explode(20,128,0,0,48)
TNT1 A 10 A_SpawnItemEx("VenusExplosiveJumpFX",0,0,12,0,0,0,0,1)
stop
Death:
VENB X 0 A_Warp(8,0,0,0,0,24)
VENB X 0 A_GiveInventory("VenusBlastJumpPower",1,8)
VENB X 0 A_TakeInventory("VenusBlastJumpFlag",9,0,8)
VENB X 0 A_PlaySoundEX("classes/burstpop","Weapon")
VENB X 3
stop
}
}

actor VenusExplosiveJumpFX : BasicExplosion
{
Scale 3.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(256,"Spawn1","Spawn2")
Spawn1:
X_VC FGHIJKLMNO 1
stop
Spawn2:
X_VC FGPQRSTUVO 1
stop
}
}

actor VenusExplosiveJumpOwn_H : BasicExplosion
{
damagetype "BubbleBomb"
Obituary "$OB_VENUSSHIELDJ"
Scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("W_Venus/BubbGet","Weapon")
TNT1 A 0 A_SetArg(4,88)
goto BoomWait
BoomWait:
VENB R 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",2)
VENB R 1 A_Warp(2,0,0,0,0,24)
VENB R 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",2)
VENB R 1 A_Warp(2,0,0,0,0,24)
VENB S 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",2)
VENB S 1 A_Warp(2,0,0,0,0,24)
VENB S 0 A_JumpIfInventory("VenusBlastJumpPower",1,"BoomJump",2)
VENB S 1 A_Warp(2,0,0,0,0,24)
VENB R 0 A_JumpIf(ACS_ExecuteWithResult(C_CHECK_TARGET_OOC)==0,"Death")
VENB R 0 A_CountDownArg(4)
loop
BoomJump:
TNT1 A 0 A_Warp(2,0,0,16,0,24)
TNT1 A 0 A_PlaySound("W_Venus/JumpBoost",7,1.0)
TNT1 A 0 A_Explode(18,128,0,0,48)
TNT1 A 10 A_SpawnItemEx("VenusExplosiveJumpFX",0,0,12,0,0,0,0,1)
stop

Death:
VENB X 0 A_GiveToTarget("VenusBlastJumpPower",1)
VENB X 0 A_TakeFromTarget("VenusBlastJumpFlag",9)
VENB X 0 A_PlaySoundEX("classes/burstpop","Weapon")
VENB X 3
stop
}
}

actor VenusAcidBubble
{
translation "250:250=105:105","249:249=112:112"
PROJECTILE
damagetype "BubbleBomb"
Obituary "$OB_VENUSSHIELD"
+HITTRACER
+THRUGHOST
damage (10)
Radius 20
Height 20
speed 20
scale 2.5
States
{
Spawn:
VENB TU 4
VENB VW 3
goto Spawn+2
Crash:
XDeath:
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("cbm_istracerplayer")==1,"DeathAcidCheck")
goto DeathPop
DeathAcidCheck:
TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(975,1)==1,"DeathAcidCheckT")
goto DeathAcidSpawn
DeathAcidCheckT:
TNT1 A 0 A_JumpIfInventory("VenusBlastJumpFlag",1,"DeathPop",8)
TNT1 A 0 A_JumpIfInventory("VenusBlastJumpPower",1,"DeathPop",8)
TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(143,ACS_ExecuteWithResult(147,0),1) == ACS_ExecuteWithResult(143,ACS_ExecuteWithResult(147,1),1),"TeamAcidSpawn")
goto DeathAcidSpawn
DeathAcidSpawn:
TNT1 A 0 A_GiveInventory("VenusAcidCoat_P",1,8)
goto DeathPop
TeamAcidSpawn:
TNT1 A 0 A_GiveInventory("VenusBlastJumpFlag",1,8)
TNT1 A 0 A_SpawnItemEx("VenusExplosiveJump_H",0,0,0,0,0,0,0,1025)
TNT1 A 1
stop
DeathPop:
VENB X 0 A_PlaySoundEX("classes/burstpop","Weapon")
VENB X 3
stop
Death:
TNT1 A 1 A_SpawnItemEx("VenusAcidBubble2",0,0,0,0,0,0,0,1)
stop
}
}

actor VenusAcidBubbleB : VenusAcidBubble{Translation"250:250=196:196","249:249=199:199"}
actor VenusAcidBubbleR : VenusAcidBubble{Translation"250:250=170:170","249:249=43:43"}
actor VenusAcidBubbleO : VenusAcidBubble{Translation"250:250=138:138","249:249=131:131"}
actor VenusAcidBubbleP : VenusAcidBubble{Translation"250:250=214:214","249:249=223:223"}

actor VenusAcidBubble2 : VenusAcidBubble
{
+DONTBLAST
ReactionTime 35
States
{
Spawn:
VENB VW 4
goto SpawnL
SpawnL:
VENB V 1 A_JumpIfCloser(40,"BubCheck1")
VENB V 1 A_JumpIfCloser(40,"BubCheck2")
VENB V 1 A_JumpIfCloser(40,"BubCheck3")
VENB W 1 A_JumpIfCloser(40,"BubCheck4")
VENB W 1 A_JumpIfCloser(40,"BubCheck5")
VENB W 1 A_JumpIfCloser(40,"BubCheck6")
VENB V 0 A_CountDown
loop

BubCheck1:
VENB V 0 A_JumpIfInventory("VenusBlastJumpFlag",1,2,2)
VENB V 0 A_JumpIfInventory("VenusBlastJumpPower",1,1,2)
goto GiveBubb
VENB V 1
goto SpawnL+1
BubCheck2:
VENB V 0 A_JumpIfInventory("VenusBlastJumpFlag",1,2,2)
VENB V 0 A_JumpIfInventory("VenusBlastJumpPower",1,1,2)
goto GiveBubb
VENB V 1
goto SpawnL+2
BubCheck3:
VENB V 0 A_JumpIfInventory("VenusBlastJumpFlag",1,2,2)
VENB V 0 A_JumpIfInventory("VenusBlastJumpPower",1,1,2)
goto GiveBubb
VENB V 1
goto SpawnL+3
BubCheck4:
VENB W 0 A_JumpIfInventory("VenusBlastJumpFlag",1,2,2)
VENB W 0 A_JumpIfInventory("VenusBlastJumpPower",1,1,2)
goto GiveBubb
VENB W 1
goto SpawnL+4
BubCheck5:
VENB W 0 A_JumpIfInventory("VenusBlastJumpFlag",1,2,2)
VENB W 0 A_JumpIfInventory("VenusBlastJumpPower",1,1,2)
goto GiveBubb
VENB W 1
goto SpawnL+5
BubCheck6:
VENB W 0 A_JumpIfInventory("VenusBlastJumpFlag",1,2,2)
VENB W 0 A_JumpIfInventory("VenusBlastJumpPower",1,1,2)
goto GiveBubb
VENB W 1
goto SpawnL+6

GiveBubb:
TNT1 A 0 A_GiveToTarget("VenusBlastJumpFlag",1)
TNT1 A 0 A_SpawnItemEx("VenusExplosiveJumpOwn_H",0,0,0,0,0,0,0,1)
stop

Death:
TNT1 A 0 A_JumpIfInventory("Once",1,"GiveBubb")
VENB X 0 A_PlaySoundEX("classes/burstpop","Weapon")
VENB X 3
stop
}
}

actor PowerVenusAcidCoatG : BasicPowerupGiver
{
powerup.type "PowerVenusAcidCoat"
}

actor PowerVenusAcidCoat : PowerProtection
{
DamageFactor "BubbleBomb", 1.3
DamageFactor "BubbleBombB", 1.5
Powerup.Duration 200
}

actor VenusAcidCoat_H : BasicHelper
{
States
{
Spawn:
TNT1 A 2 A_GiveToTarget("VenusAcidCoat_P1",1)
TNT1 A 1 A_JumpIfInTargetInventory("PowerVenusAcidCoat",1,1)
stop
TNT1 A 2 A_GiveToTarget("VenusAcidCoat_P1",1)
TNT1 A 1 A_JumpIfInTargetInventory("PowerVenusAcidCoat",1,1)
stop
TNT1 A 2 A_GiveToTarget("VenusAcidCoat_P1",1)
TNT1 A 1 A_JumpIfInTargetInventory("PowerVenusAcidCoat",1,1)
stop
TNT1 A 2 A_GiveToTarget("VenusAcidCoat_P2",1)
TNT1 A 1 A_JumpIfInTargetInventory("PowerVenusAcidCoat",1,"Spawn")
stop
}
}

actor VenusAcidCoat_P : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("PowerVenusAcidCoat",1,2)
TNT1 A 0 A_SpawnItemEx("VenusAcidCoat_H",0,0,24,0,0,0,0,1)
TNT1 A 0 A_GiveInventory("PowerVenusAcidCoatG",1)
stop
}
}

actor VenusAcidCoat_P1 : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_SpawnItemEX("VenusAcidCoat_Drip",random(-24,24),random(-24,24),random(8,32))
stop
}
}

actor VenusAcidCoat_P2 : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_SpawnItemEX("VenusAcidCoat_Drip2",random(-24,24),random(-24,24),random(8,32))
stop
}
}

actor VenusAcidCoat_Drip
{
Translation "192:192=105:105","198:198=112:112"
+MISSILE
+DONTBLAST
+THRUACTORS
+DONTSPLASH
-NOGRAVITY
+CLIENTSIDEONLY
ReactionTime 4
Radius 4
Height 4
Speed 10
Scale 1.0
States
{
Spawn:
ACID T 4 A_CountDown
loop
Death:
ACID UV 5
stop
}
}

actor VenusAcidCoat_Drip2 : VenusAcidCoat_Drip
{
States
{
Death:
ACID U 0 A_PlaySound("wep_L/OilShot",2,0.25)
goto Super::Death
}
}

actor VenusMain1_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("VenusBubbleTicker",frandom(-3,3),1,-8,0,0,frandom(-3,3))
TNT1 A 0 A_FireCustomMissile("VenusBubbleTicker",frandom(-3,3),0,-8,0,0,frandom(-3,3))
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerB",frandom(-3,3),1,-8,0,0,frandom(-3,3))
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerB",frandom(-3,3),0,-8,0,0,frandom(-3,3))
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerR",frandom(-3,3),1,-8,0,0,frandom(-3,3))
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerR",frandom(-3,3),0,-8,0,0,frandom(-3,3))
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerO",frandom(-3,3),1,-8,0,0,frandom(-3,3))
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerO",frandom(-3,3),0,-8,0,0,frandom(-3,3))
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerP",frandom(-3,3),1,-8,0,0,frandom(-3,3))
TNT1 A 0 A_FireCustomMissile("VenusBubbleTickerP",frandom(-3,3),0,-8,0,0,frandom(-3,3))
goto Done
}
}

actor VenusMain2_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("VenusBubbleBomb",0,1,-8,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("VenusBubbleBombB",0,1,-8,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("VenusBubbleBombR",0,1,-8,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("VenusBubbleBombO",0,1,-8,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("VenusBubbleBombP",0,1,-8,0)
goto Done
}
}

actor VenusAlt_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("VenusAcidBubble",0,1,0,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("VenusAcidBubbleB",0,1,0,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("VenusAcidBubbleR",0,1,0,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("VenusAcidBubbleO",0,1,0,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("VenusAcidBubbleP",0,1,0,0)
goto Done
}
}