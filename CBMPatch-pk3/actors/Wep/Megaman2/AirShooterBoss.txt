actor AirShooterBoss : BaseMM8BDMWep_CBM
{
tag "$TAGC_2B"
dropitem "AirShooterWepCDropped"
Weapon.AmmoUse 0
Weapon.AmmoGive 28
Obituary "$OB_AIRSHOOTER"
weapon.ammotype "Can'tDefeatAmmo"
States
{
Spawn:
C_02 B 1
loop
Ready:
AIRA A 0 ACS_ExecuteAlways(998,0,DYE_AIRMAN)
AIRA A 0 A_GunFlash
goto Ready2
Ready2:
AIRA A 0 A_JumpIfInventory("Can'tDefeatAmmo",4,"Ready3")
AIRA A 5 A_WeaponReady(8)
AIRA A 0 A_GiveInventory("AirAmmoCheck",1)
loop
Ready3:
AIRA A 5 A_WeaponReady
AIRA A 0 A_GiveInventory("AirAmmoCheck",1)
AIRA A 0 A_JumpIfInventory("Can'tDefeatAmmo",4,"Ready3")
AIRA A 1 A_Jump(256,"Ready2")
wait
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
AIRA A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
AIRA A 1 A_Raise
Loop
Fire:
TNT1 A 0 A_JumpIfInventory("IsBot",1,"BotFire")
AIRA A 0 A_PlaySoundEx("weapon/airshooter","Weapon")
AIRA A 0 A_GiveInventory("AirmanMain_CI",1)
AIRA BC 6
AIRA A 7
Goto Ready2
AltFire:
AIRA A 0 A_JumpIfInventory("Can'tDefeatAmmo",4,"AirWall")
goto Ready2
AirWall:
AIRA A 0 A_GiveInventory("VivifyDelay6",1)
AIRA A 0 A_Refire(1)
AIRA A 0 A_PlaySoundEx("weapon/airwall","Body")
AIRA A 0 A_TakeInventory("Can'tDefeatAmmo",4)
AIRA A 0 A_GiveInventory("AirmanAlt_CI",1)
AIRA BC 6
AIRA A 7
Goto Ready2

NoAmmo:
AIRA A 0
Goto Ready2

BotFire:
AIRA A 0 A_JumpIfCloser(350,"BotFire2")
AIRA A 0 A_JumpIfInventory("Can'tDefeatAmmo",4,"AirWall")
AIRA A 5
AIRA A 0 A_GiveInventory("Can'tDefeatAmmo",1)
goto AltFire
BotFire2:
TNT1 A 0 A_Jump(192,"BotFire3")
goto BotFire+1
BotFire3:
AIRA A 0
goto Fire+1

EndFlash:
TNT1 A 0 A_TakeInventory("AirTurbineCounter",999)
TNT1 A 0 A_PlaySoundEx("S3_K/MetalSpark","SoundSlot6")
Flash:
TNT1 A 0 ACS_ExecuteAlways(CORE_ACS_191,0,APROP_JumpZ,10,1)
TNT1 A 0 ACS_ExecuteAlways(CORE_ACS_191,0,APROP_Speed,1,1)
TNT1 A 1 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 0 A_JumpIfInventory("AirTurbineNotActive",1,"Flash")
TNT1 A 1 A_Jump(256,"StartFlash")
wait
StartFlash:
TNT1 A 0 ACS_ExecuteAlways(CORE_ACS_191,0,APROP_JumpZ,14,1)
FlashJumpEnd:
TNT1 A 0 ACS_ExecuteAlways(CORE_ACS_191,0,APROP_Speed,1,1)
FlashActive:
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 0 A_GiveInventory("AirAmmoTaker",1)
TNT1 A 1 A_JumpIfInventory("AirTurbineNotActive",1,"EndFlash")
TNT1 A 0 A_CheckFloor("FlashActive")
TNT1 A 1 A_Jump(256,"FlashJumpStart")
wait
FlashJumpStart:
TNT1 A 0 ACS_ExecuteAlways(CORE_ACS_191,0,APROP_Speed,2,1)
FlashJumpActive:
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
TNT1 A 0 A_GiveInventory("AirAmmoTaker",1)
TNT1 A 1 A_JumpIfInventory("AirTurbineNotActive",1,"EndFlash")
TNT1 A 0 A_CheckFloor("FlashJumpEnd")
TNT1 A 1 A_Jump(256,"FlashJumpActive")
wait
NoFlash:
TNT1 A 0
stop
}
}

actor Can'tDefeatAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor AirTurbineItem : BasicClassItem
{
inventory.icon "AIRICON"
inventory.pickupmessage "ARBABRRBAL"
Tag ""
states
{
Spawn:
AIWI I 1
loop
Use:
TNT1 A 0 A_JumpIfInventory("AirTurbineNotActive",1,"ChangeState")
TNT1 A 0 A_TakeInventory("AirTurbineCounter",999)
TNT1 A 0 A_TakeInventory("Can'tDefeatAmmo",1)
TNT1 A 0 A_GiveInventory("AirTurbineNotActive",1)
fail
ChangeState:
TNT1 A 0 A_JumpIfInventory("Can'tDefeatAmmo",2,1)
goto UseNope
TNT1 A 0 A_PlaySoundEx("class/windstart","SoundSlot6")
TNT1 A 0 A_TakeInventory("Can'tDefeatAmmo",1)
TNT1 A 0 A_TakeInventory("AirTurbineNotActive",999)
fail
UseNope:
TNT1 A 0
fail
}
}

actor AirAmmoCheck : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("AirTurbineNotActive",1,1)
Goto PickupNo
TNT1 A 0 A_GiveInventory("Can'tDefeatAmmo",1)
goto PickupNo
PickupNo:
TNT1 A 0
stop
}
}

actor AirAmmoTaker : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("Can'tDefeatAmmo",1,1)
goto PickupEnd
TNT1 A 0 A_GiveInventory("AirTurbineCounter",1)
TNT1 A 0 A_CheckFloor("FXNope")
TNT1 A 0 A_SpawnItemEX("AirTurbineFX",random(32,64),0,random(0,64),0,0,0,random(90,270),SXF_TRANSFERTRANSLATION)
FxNope:
TNT1 A 0 A_JumpIfInventory("AirTurbineCounter",0,"PickupTake")
goto PickupNo
PickupNo:
TNT1 A 0
stop
PickupTake:
TNT1 A 0 A_TakeInventory("AirTurbineCounter",999)
TNT1 A 0 A_TakeInventory("Can'tDefeatAmmo",1)
TNT1 A 0 A_JumpIfInventory("Can'tDefeatAmmo",1,1)
goto PickupEnd
TNT1 A 0
goto PickupNo
PickupEnd:
TNT1 A 0 A_GiveInventory("AirTurbineNotActive",1)
goto PickupNo
}
}

actor AirTurbineFX : BasicClientSide
{
Scale 2.25
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3")
Spawn1:
AIWI FGH 3
stop
Spawn2:
AIWI G 4
AIWI H 3
stop
Spawn3:
AIWI F 1
AIWI G 3
AIWI H 4
stop
}
}

actor AirTurbineCounter : Inventory
{
inventory.amount 1
inventory.maxamount 10
}

actor AirTurbineNotActive : OnceC {}

/*
actor AirWind
{
PROJECTILE
Radius 32
Height 24
scale 2.5
speed 40
States
{
Spawn:
AIWI ABCD 3 A_RadiusThrust (740000, 1, 0)
loop
}
}
*/

actor Airman_AirShot
{
PROJECTILE
damagetype "AirShooter"
Obituary "$OB_AIRSHOOTER"
+NOEXPLODEFLOOR
damage (12)//20
Radius 6
Height 7
speed 28//22
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(256,1,2,3,4,5,6)
AIRS AABBCC 1 ThrustThingZ(0,2,0,1)
goto Spawn+2
Death:
AIRS GHIJ 2
stop
}
}

actor Airman_AirShotB : Airman_AirShot{translation "198:198=74:74"}
actor Airman_AirShotR : Airman_AirShot{translation "198:198=41:41"}
actor Airman_AirShotO : Airman_AirShot{translation "198:198=128:128"}
actor Airman_AirShotP : Airman_AirShot{translation "198:198=232:232"}

actor Airman_AirWall_H : BasicHelper
{
States
{
Spawn:
AIRS A 0
AIRS A 0 A_SpawnItemEx("Airman_AirWall",2,0,0,15,0,0,0,1)
AIRS A 0 A_SpawnItemEx("Airman_AirWall",2,0,0,15,random(4,8),random(-2,2),0,1)
AIRS A 0 A_SpawnItemEx("Airman_AirWall",2,0,0,15,random(-8,-4),random(-2,2),0,1)
AIRS A 0 A_SpawnItemEx("Airman_AirWall",2,0,0,15,random(12,16),random(-3,3),0,1)
AIRS A 0 A_SpawnItemEx("Airman_AirWall",2,0,0,15,random(-16,-12),random(-3,3),0,1)
AIRS A 0 A_SpawnItemEx("Airman_AirWall",2,0,0,15,random(20,24),random(-4,4),0,1)
AIRS A 0 A_SpawnItemEx("Airman_AirWall",2,0,0,15,random(-24,-20),random(-4,4),0,1)
stop
}
}

actor Airman_AirWall_HB : Airman_AirWall_H{translation "198:198=74:74"}
actor Airman_AirWall_HR : Airman_AirWall_H{translation "198:198=41:41"}
actor Airman_AirWall_HO : Airman_AirWall_H{translation "198:198=128:128"}
actor Airman_AirWall_HP : Airman_AirWall_H{translation "198:198=232:232"}

actor Airman_AirWall : Airman_AirShot
{
var int user_A;
damagetype "AirShooterWall"
-NOEXPLODEFLOOR
+BOUNCEONFLOORS
+BOUNCEONWALLS
+BOUNCEONCEILINGS
BounceFactor 0.5
WallBounceFactor 0.5
damage (9)
Radius 8
Height 10
speed 0
States
{
Spawn:
AIRS A 0
AIRS A 0 A_SetUserVar("user_A",ACS_ExecuteWithResult(259,4)*360/256)//Record Player Angle
AIRS A 0 ACS_ExecuteAlways(259,0,1)//Player Pitch
AIRS AABBCC 1 A_Recoil(3)
AIRS A 0 A_Stop
AIRS ABC 2
AIRS A 0 A_SpawnItemEx("Airman_AirWall2",0,0,0,cos(-pitch)*22,0,sin(-pitch)*22,user_A,5)
stop
}
}

actor AirShooterWallProtect : PowerProtection
{
Powerup.Duration 4
DamageFactor "AirShooterWall", 0.19
}

actor Airman_AirWall2 : Airman_AirShot
{
damagetype "AirShooterWall"
-NOEXPLODEFLOOR
damage (15)
Radius 8
Height 10
speed 22
States
{
Spawn:
AIRS AABBCC 1 A_ScaleVelocity(1.05)
loop
}
}

actor AirmanMain_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("Airman_AirShot",-18,0,-8,0)//-18
TNT1 A 0 A_FireCustomMissile("Airman_AirShot",18,0,-8,0)//15
TNT1 A 0 A_FireCustomMissile("Airman_AirShot",0,1,-8,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("Airman_AirShotB",-18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotB",18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotB",0,1,-8,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("Airman_AirShotR",-18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotR",18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotR",0,1,-8,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("Airman_AirShotO",-18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotO",18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotO",0,1,-8,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("Airman_AirShotP",-18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotP",18,0,-8,0)
TNT1 A 0 A_FireCustomMissile("Airman_AirShotP",0,1,-8,0)
goto Done
}
}



actor AirmanAlt_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("Airman_AirWall_H",0,1,-8,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("Airman_AirWall_HB",0,1,-8,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("Airman_AirWall_HR",0,1,-8,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("Airman_AirWall_HO",0,1,-8,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("Airman_AirWall_HP",0,1,-8,0)
goto Done
}
}
